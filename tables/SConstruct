import os
import sys

# Put the package name here, e.g. casa, tables
PACKAGE = "tables"
DEPS = ["casa"]

# general options
opts = Variables( 'options.cfg', ARGUMENTS )
opts.Add(BoolVariable("enable_readline", "Enable readline support", False))
opts.Add(PathVariable("casacoreroot", "The location of casacore/casa",
		    "/usr/local"))
opts.Add(("hdf5root", 
	  "The root directory where hdf5 is installed", None))
opts.Add(("hdf5incdir", "The hdf5 header location", None))
opts.Add(("hdf5libdir", "The hdf5 library location", None))

env = Environment(ENV = { 'PATH' : os.environ[ 'PATH' ],
			  'HOME' : os.environ[ 'HOME' ] 
			  },
		  variables=opts
		  )

# keep a local sconsign database, rather than in every directory
env.SConsignFile()

env["PACKAGE"] = PACKAGE
# use directory from casacore/casa installation
env["casashrdir"] = [os.path.join(env["casacoreroot"],"share","casacore")]

env.Tool('casaoptions', env["casashrdir"])
# Add common options
env.AddCommonOptions(opts)
env.Tool('installer', env["casashrdir"])
# add installer options, e.g. prefix
env.AddInstallerOptions( opts )
# add them into environment
opts.Update( env )
# cache them for the next run
opts.Save( 'options.cfg', env)
Help( opts.GenerateHelpText( env ) )

env.Tool('buildenv', env["casashrdir"])
env.Tool('utils', env["casashrdir"])
env.Tool('casa', env["casashrdir"])
env.Tool('assaytest', env["casashrdir"])

env["casaincdir"] = [os.path.join(env["casacoreroot"],"include","casacore")]
env["casalibdir"] = [os.path.join(env["casacoreroot"],env.ARCHLIBDIR)]


env["ASSAYCOM"] = os.path.join(env["prefix"], "bin", "casacore_assay")

if not env.GetOption('clean'):
    conf = Configure(env)

    # HDF5 is not mandatory
    conf.env.AddCustomPackage('hdf5')
    if conf.CheckLib('hdf5', autoadd=0):
        conf.env.PrependUnique(LIBS=['hdf5'])  
    else:
        print "Building without HDF5 support"

    if conf.env.get("enable_readline") and \
             conf.CheckLib('readline', autoadd=0):
        conf.env.AppendUnique(CPPFLAGS=["-DHAVE_READLINE"])
        conf.env.PrependUnique(LIBS=['readline'])

    if not conf.CheckLib("dl"): Exit(1)
    conf.env.PrependUnique(LIBS=['dl'])
    env = conf.Finish()
else:
    Execute(Delete("options.cfg"))

# create the installer which handles installing the final build
installer = env.Installer()

# to find package based includes
env.Append(CPPPATH=['#'])
env.AppendUnique(CPPPATH=env["casaincdir"])
env.AppendUnique(LIBPATH=env["casalibdir"])
for dep in DEPS:
    env.Prepend(LIBS=["casa_"+dep])

for bopt in env["build"]:
    # create an environment copy with teh dbg/opt compiler flags
    buildenv = env.BuildEnv(bopt)
    buildenv["buildtype"] = bopt
    # buildir name
    buildenv["BUILDDIR"] = Dir("#/build_%s/%s" % (env.PlatformIdent(), bopt))
    buildenv["BUILDDIR_APPS"] = Dir("#/build_%s/%s/%s" % (env.PlatformIdent(), 
						      bopt, "apps"))

    env.SConscript(["%s/SConscript" % env["PACKAGE"]], 
		   build_dir= buildenv["BUILDDIR"],
		   duplicate=0, exports=["buildenv", "installer"]) 
    env.SConscript(["%s/SConscript" % "apps"],
		   duplicate=0, exports=["buildenv", "installer"],
		   build_dir=buildenv["BUILDDIR_APPS"])
