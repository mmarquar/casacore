/*
    ImageExprGram.l: Lexical analyzer for image expressions
    Copyright (C) 1998
    Associated Universities, Inc. Washington DC, USA.

    This library is free software; you can redistribute it and/or modify it
    under the terms of the GNU Library General Public License as published by
    the Free Software Foundation; either version 2 of the License, or (at your
    option) any later version.

    This library is distributed in the hope that it will be useful, but WITHOUT
    ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
    FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Library General Public
    License for more details.

    You should have received a copy of the GNU Library General Public License
    along with this library; if not, write to the Free Software Foundation,
    Inc., 675 Massachusetts Ave, Cambridge, MA 02139, USA.

    Correspondence concerning AIPS++ should be addressed as follows:
           Internet email: aips2-request@nrao.edu.
           Postal address: AIPS++ Project Office
                           National Radio Astronomy Observatory
                           520 Edgemont Road
                           Charlottesville, VA 22903-2475 USA

    $Id$
*/

%{
#undef YY_INPUT
#define YY_INPUT(buf,result,max_size) result=imageExprGramInput(buf,max_size)

#undef YY_DECL
#define YY_DECL int ImageExprGramlex (YYSTYPE* lvalp)
%}

/*
   Complex values can be given as:   FLOATi
          where i is the letter i (in lowercase only).
   In a NAME the backslash can be used to escape special characters like -.
   In that way a name like DIR/NAME can be given as DIR\/NAME.
   Alternatively the name can be enclosed in single or double quotes.
*/
WHITE     [ \t\n]*
DIGIT     [0-9]
INT       {DIGIT}+
FEXP      [Ee][+-]?{INT}
DEXP      [Dd][+-]?{INT}
FLOAT     {INT}{FEXP}|{INT}"."{DIGIT}*({FEXP})?|{DIGIT}*"."{INT}({FEXP})?
DOUBLE    {INT}{DEXP}|{INT}"."{DIGIT}*({DEXP})?|{DIGIT}*"."{INT}({DEXP})?
FLINT     {FLOAT}|{INT}
DBINT     {DOUBLE}|{INT}
COMPLEX   {FLINT}"i"
DCOMPLEX  {DBINT}"i"
TRUE      T
FALSE     F

QSTRING   \"[^\"\n]*\"
ASTRING   \'[^\'\n]*\'
UQSTRING   \"[^\"\n]*\n
UASTRING   \'[^\'\n]*\n
STRING    ({QSTRING}|{ASTRING})+
USTRING   ({UQSTRING}|{UASTRING})+
NAME      [A-Za-z_]([A-Za-z_0-9])*
TEMPNAME  [$]{INT}
ESCNAME   ([A-Za-z0-9._~$]|(\\.))+


%%

 /* Literals */
{DCOMPLEX} {
            imageExprGramPosition() += yyleng;
            Double value;
	    sscanf (ImageExprGramtext, "%lf%*c", &value);
            lvalp->val = new ImageExprParse (DComplex (0, value));
	    return LITERAL;
	  }
{COMPLEX} {
            imageExprGramPosition() += yyleng;
            Float value;
	    sscanf (ImageExprGramtext, "%f%*c", &value);
            lvalp->val = new ImageExprParse (Complex (0, value));
	    return LITERAL;
	  }
{DOUBLE}  {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (atof(ImageExprGramtext));
	    return LITERAL;
	  }
{FLOAT}   {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (Float(atof(ImageExprGramtext)));
	    return LITERAL;
	  }
{INT}     {
            imageExprGramPosition() += yyleng;
            Int ival = atoi(ImageExprGramtext);
            Double dval = atof(ImageExprGramtext);
            if (ival < dval-0.1  ||  ival > dval+0.1) {
                lvalp->val = new ImageExprParse (dval);
            } else {
                lvalp->val = new ImageExprParse (ival);
            }
            return LITERAL;
	  }
{TRUE}    {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (True);
	    return LITERAL;
	  }
{FALSE}   {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (False);
	    return LITERAL;
	  }

 /* A simple name can be name of constant, function, or lattice */
{NAME}    {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (ImageExprGramtext);
	    return NAME;
	  }

 /* A temporary image number can be given */
{TEMPNAME} {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse (atoi(ImageExprGramtext+1));
	    return NAME;
	  }

 /*
 A lattice name can have more characters than a simple name
 Note that the name of a lattice file can be given in 3 ways:
 - When it contains only alphanumerical characters and ._~$
   it can be given as such. E.g.
       a.img
 - When other characters are used, they have to be escaped.
   This can be done in 2 ways:
   - Enclose the string in single or double quotes (concatenation is
     possible). E.g.
       "a/b/c"      results in a/b/c
       "a'b"'c"d'   results in a'bc"d
   - Use the backslash escape character. E.g.
       a\/b\/c      results in a/b/c
 */
{ESCNAME} {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse
                             (imageExprGramRemoveEscapes (ImageExprGramtext));
	    return LATNAME;
	  }
{STRING}  {
            imageExprGramPosition() += yyleng;
            lvalp->val = new ImageExprParse
                             (imageExprGramRemoveQuotes (ImageExprGramtext));
	    return LATNAME;
	  }

"=="      { imageExprGramPosition() += yyleng; return EQ; }
">="      { imageExprGramPosition() += yyleng; return GE; }
">"       { imageExprGramPosition() += yyleng; return GT; }
"<="      { imageExprGramPosition() += yyleng; return LE; }
"<"       { imageExprGramPosition() += yyleng; return LT; }
"!="      { imageExprGramPosition() += yyleng; return NE; }
"&&"      { imageExprGramPosition() += yyleng; return AND; }
"||"      { imageExprGramPosition() += yyleng; return OR; }
"!"       { imageExprGramPosition() += yyleng; return NOT; }
"^"       { imageExprGramPosition() += yyleng; return POWER; }
"*"       { imageExprGramPosition() += yyleng; return TIMES; }
"/"       { imageExprGramPosition() += yyleng; return DIVIDE; }
"%"       { imageExprGramPosition() += yyleng; return MODULO; }
"+"       { imageExprGramPosition() += yyleng; return PLUS; }
"-"       { imageExprGramPosition() += yyleng; return MINUS; }
"("       { imageExprGramPosition() += yyleng; return LPAREN; }
")"       { imageExprGramPosition() += yyleng; return RPAREN; }
","       { imageExprGramPosition() += yyleng; return COMMA; }

 /* Whitespace is skipped */
{WHITE}   { imageExprGramPosition() += yyleng; }

 /* An unterminated string is an error */
{USTRING} { throw (AipsError ("ImageExprParse: Unterminated string")); }

 /* terminate on EOF */
<<EOF>>   { yyterminate(); }

 /* Any other character is invalid */
.         { return YYERRCODE; }

%%
