import os
import sys

# Put the package name here, e.g. casa, tables
PACKAGE="casa"

env = Environment(ENV = { 'PATH' : os.environ[ 'PATH' ],
			  'HOME' : os.environ[ 'HOME' ] 
			  }
		  )

# general options
opts = Options( 'options.cfg', ARGUMENTS )
opts.Add(ListOption("build", "The build type", 
		    "opt", ["opt", "dbg"]))
opts.Add(ListOption("libtype", "The type of library to build", 
		    "static", ["static", "shared"]))
opts.Add(BoolOption("tests", "Build the tests?", False))
opts.Add(PathOption("casacoredir", "The location of casacore/casa",
		    "/usr"))
# add them into the environment
opts.Update( env )
# cache them for the next run
opts.Save( 'options.cfg', env)
Help( opts.GenerateHelpText( env ) )
env.SConsignFile()

env["PACKAGE"] = PACKAGE
if env["PACKAGE"] == "casa":
    # use build in dir
    env["casashrdir"] = ["./tools"]
else:
    # use directory from casacore/casa installation
    env["casaincdir"] = [os.path.join(env["casacoredir"],"include","casacore")]

env.Tool('buildenv', env["casashrdir"])
env.Tool('utils', env["casashrdir"])
env.Tool('installer', env["casashrdir"])
env.Tool('casa', env["casashrdir"])
# create the installer
install = env.Installer()
# add installer options, e.g. prefix
env.AddInstallerOptions( opts )
# to find package based includes
env.Append(CPPPATH='#')
env.AddCasaPlatform()
for bopt in env["build"]:
    buildenv = env.BuildEnv(bopt)
    buildenv["BUILDDIR"] = Dir("#/build_%s/%s" % (env.PlatformIdent(), bopt))
    env.SConscript(["%s/SConscript" % env["PACKAGE"]], 
		   build_dir= buildenv["BUILDDIR"],
		   duplicate=0, exports=["buildenv", "install"]) 
    
# add the Tools to the casacore/share directory. This way they can be imported
# in the other casacore packages, without having to duplicate them.
if env["PACKAGE"] == "casa":
    install.AddShare(".", "*.py", "casacore", True)


