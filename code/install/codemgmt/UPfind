#!/usr/local/bin/tcsh
#-----------------------------------------------------------------------------
# UPfind script
#-----------------------------------------------------------------------------
# Copyright (C) 2000
# Associated Universities, Inc. Washington DC, USA.
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 675 Massachusetts Ave, Cambridge, MA 02139, USA.
#
# Correspondence concerning AIPS++ should be addressed as follows:
#        Internet email: aips2-request@nrao.edu.
#        Postal address: AIPS++ Project Office
#                        National Radio Astronomy Observatory
#                        520 Edgemont Road
#                        Charlottesville, VA 22903-2475 USA
#
# $Id$
#
set script = "UPfind"
set usage = 'Usage: UPfind [-c] [-g] <typ>'
#
# UPfind script (must have x allowed)
# Get all files that have to be updated according to an egrep rule
# All .cc, .h and template files are scanned, or the .g if -g given
# The file list will be in <typ>.raw
# <typ>.found will have the list without user selected
# removals.
#
# See UPinfo for details
#
# Get general info
#
if (! $?AIPSPATH) then
   echo "No AIPSPATH environment set, initialize aips++ first"
   exit 1
endif
set x = "`which UPfind`"
if (-x "$x") then
   set uppath = $x:h
else
   set x = ( $AIPSPATH )
   set uppath = $x[1]/$x[2]/bin
endif
unset x
if ("$uppath" == ".") then
    set uppath = `pwd`
endif
if (! -r $uppath/UPinfo) then
   echo "Cannot find the UPinfo source script in $uppath"
   echo "Check your aips++ installation"
endif
source $uppath/UPinfo
if (! $ok) exit 1
#
# Get specific data
#
if (! -r $ldir/${tp}.up1) then
   touch $ldir/${tp}.up1
   echo "Specify egrep  expression(s) to find files"
   echo 'Examples: /Measures/((MUString)|(MVAngle)|(MVTime)|(RotM)|Q|(Un))'
   echo '      or: [(][^(]*[*] *[)][ \t]*0'
   echo 'Multi-line expressions will be ORed'
   while (1)
	echo -n "Find expression: "
	set x = "$<"
	if ("$x" == "") break
	echo "$x" >> $ldir/${tp}.up1
   end
   unset x
endif

if (! -r $ldir/${tp}.rm) then
   echo "Specify egrep pattern for files to be removed from list found."
   echo "Example: [/]glish[/]"
   echo "tmplinst files will always be removed"
   echo tmplinst >! $ldir/${tp}.rm
   echo -n "Remove glish files? (y|n) [y]: "
   set x = "$<"
   if ("$x" != "n") then
      echo '[/]glish[/]' >> $ldir/${tp}.rm
   endif
   while (1)
	 echo -n "Remove pattern: "
	 set x = "$<"
	 if ("$x" == "") break
	 echo "$x" >> $ldir/${tp}.rm
   end
   unset x
endif

if (-z $ldir/${tp}.up1) then
   echo "No ${tp} up1 file created"
   exit (1)
endif

cd $dir/code
echo "Starting find..."
if ($gls) then
   find . -name '*.g' \
	-exec egrep -l -f $ldir/${tp}.up1 {} \; \
	      >! $ldir/${tp}.raw 
else
   find . \( \( -name '*.h' -o -name '*.cc' \) -o -name 'templates' \) \
	-exec egrep -l -f $ldir/${tp}.up1 {} \; \
	      >! $ldir/${tp}.raw 
endif
cd $ldir

set a = ( `wc $ldir/${tp}.raw` )
echo $a[1] files found in ${tp}.raw
unset a

echo "Starting selected removal..."
egrep -v -f $ldir/${tp}.rm $ldir/${tp}.raw >! $ldir/${tp}.found

set a = ( `wc ${tp}.found` )
echo $a[1] files left in ${tp}.found
unset a

exit 0






