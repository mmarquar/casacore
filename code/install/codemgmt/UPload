#!/usr/bin/env tcsh
#-----------------------------------------------------------------------------
# UPload script
#-----------------------------------------------------------------------------
# Copyright (C) 2000,2001
# Associated Universities, Inc. Washington DC, USA.
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 675 Massachusetts Ave, Cambridge, MA 02139, USA.
#
# Correspondence concerning AIPS++ should be addressed as follows:
#        Internet email: aips2-request@nrao.edu.
#        Postal address: AIPS++ Project Office
#                        National Radio Astronomy Observatory
#                        520 Edgemont Road
#                        Charlottesville, VA 22903-2475 USA
#
# $Id$
#
#
# UPload script (must have x allowed)
#
set script = "UPload"
set usage = 'UPload [-c] [-r] [-u] [-d] [-n] <typ>'
set htxt  = ("Upload the information specified by <typ> to the aips++ " \
	     "master site. This enables to run relevant scripts that" \
	     "were tested locally there.")
set huse  =  "<typ>.*"
set hcrea =  "<typ>.scp"
#
# Get general info
#
if (! $?AIPSPATH) then
   echo "No AIPSPATH environment set, initialize aips++ first"
      exit 1
endif
set x = "`which UPfind`"
if (-x "$x") then
   set uppath = $x:h
else
   set x = ( $AIPSPATH )
   set uppath = $x[1]/$x[2]/bin
endif
unset x
if ("$uppath" == ".") then
   set uppath = `pwd`
endif
if (! -r $uppath/UPinfo) then
   echo "Cannot find the UPinfo source script in $uppath"
   echo "Check your aips++ installation"
endif
source $uppath/UPinfo
if (! $ok) exit 1
#
# Get specific data 
#
set lscp = ""
if (-e $ldir/.UpScP) then
    source .UpScP
endif
if ("$lscp" == "") then
    set lscp = "${USER}@aips2.nrao.edu:aips++/test/found"
endif
echo -n "Remote directory [$lscp]: "
set x = "$<"
if ("$x" != "") set lscp = $x
if ("$lscp" == "") then
    echo "No remote directory given"
    exit 1
endif
unset x
echo "set lscp = $lscp" >! $ldir/.UpScP

if (-e $ldir/${tpb}.scp) rm -f $ldir/${tpb}.scp
cd $ldir
touch $ldir/${tpb}.scp
foreach i (${tpb}[._]*[^~])
    if ("$i" != "${tpb}.def" && "$i" != "${tpb}.scp") then
	echo $i >> $ldir/${tpb}.scp
    endif
end
if (-z $ldir/${tpb}.scp) then
    echo "An empty or no ${tpb}.scp present"
    exit 1
endif
   
if (! $upswQ) then
  set a = ( `wc $ldir/${tpb}.scp` )
  echo $a[1] files in ${tp}.scp
  unset a
endif

if ($upswn) exit 0
#
# Execute
#
if (! $upswQ) then
  set a = ( `wc $ldir/${tpb}.scp` )
  echo "Uploading $a[1] files ...."
  unset a
endif
#
# Copy files
#
if (! $upswQ) then
    scp `cat $ldir/${tpb}.scp` "$lscp"
else
    scp `cat $ldir/${tpb}.scp` "$lscp" >! /dev/null
endif

exit 0
#
