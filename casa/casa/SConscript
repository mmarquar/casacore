import os, glob

# import root environment
Import( ["buildenv", "installer"])
myenv = buildenv.Copy()
# get all the sourcefiles recursively
lls = myenv.SGlob("*.ll", recursive=True )
for f in lls:
    ff = myenv.CXXFile(File(f), CXXFILESUFFIX=".lcc")
    # hack to point include path to the builddir relative directory
    # where the lex/yacc file was found
    path = os.path.split(str(ff[0]))[0]
    myenv.AppendUnique(CPPPATH=[Dir(path)])

yys = myenv.SGlob("*.yy", recursive=True )
for f in yys:
    ff = myenv.CXXFile(File(f), CXXFILESUFFIX=".ycc")
    # hack to point include path to the builddir relative directory
    # where the lex/yacc file was found
    path = os.path.split(str(ff[0]))[0]
    myenv.AppendUnique(CPPPATH=[Dir(path)])

# get all the sourcefiles recursively
cpps = myenv.SGlob("*.cc", excludedirs=["test"], recursive=True )
# need this to go to the root dir, as SConsript asumes to be in the
# build dir
rootdir = myenv.Dir("#").abspath
libname = "casa_" + myenv["PACKAGE"]

slib = None
dlib = None
if "static" in myenv["libtype"]:
    # build static lib
    slib =  myenv.StaticLibrary(target = libname, source = [cpps])
    myenv.Default(slib)
    installer.AddLibrary(slib)

if "shared" in myenv["libtype"] and str(myenv["build"]) == "opt":
    #build dynamic lib
    dlib =  myenv.SharedLibrary(target = libname, source = cpps)
    myenv.Default(dlib)
    installer.AddLibrary(dlib)

# enable later - use targets instead of options
# slib =  myenv.StaticLibrary(target = libname, source = [cpps])
# myenv.Alias(os.path.basename(os.path.normpath(str(slib[0]))), slib)
# myenv.Alias("static", slib)
# myenv.Default(slib)
# installer.AddLibraray(slib)

# dlib =  myenv.SharedLibrary(target = libname, source = cpps)
# installer.AddLibrary(dlib)
# myenv.Alias(os.path.basename(os.path.normpath(str(dlib[0]))), dlib)
# myenv.Alias("shared", dlib)
# installer.AddLibraray(dlib)

testenv = myenv.Copy()
# gather test files
# SGlob works in build dir, so only for cc files
tests = testenv.SGlob("test/*.cc")
tests += testenv.SGlob("*/test/*.cc")
# point to tests local includes
testenv.AppendUnique(LIBPATH=[testenv["BUILDDIR"]])
testenv.AppendUnique(LIBS=[slib or dlib])
for t in tests:
    testenv.addAssayTest(t)

# install headers, only works with absolute dir.
installer.AddHeaders( rootdir, "*.h", "casacore", True )
installer.AddHeaders( rootdir, "*.tcc", "casacore", True )
