import os, glob
import SCons.Defaults

def libpath2ldpath(pth):
    ldpath=""
    for p in pth:
	if isinstance(p, str):
	    ldpath += p
	else:
	    ldpath += p.abspath 
	ldpath += os.path.pathsep
    return ldpath[:-1]

# import root environment
Import( ["buildenv", "installer"])
myenv = buildenv.Copy()
# get all the sourcefiles recursively
lls = myenv.SGlob("*.ll", recursive=True )
for f in lls:
    lenv = myenv.Copy(CXXFILESUFFIX=".lcc")
    ff = lenv.CXXFile(File(f))
    # hack to point include path to the builddir relative directory
    # where the lex/yacc file was found
    path = os.path.split(str(ff[0]))[0]
    myenv.AppendUnique(CPPPATH=[Dir(path)])

yys = myenv.SGlob("*.yy", recursive=True )
for f in yys:
    yenv = myenv.Copy(CXXFILESUFFIX=".ycc")
    ff = yenv.CXXFile(File(f))
    # hack to point include path to the builddir relative directory
    # where the lex/yacc file was found
    path = os.path.split(str(ff[0]))[0]
    myenv.AppendUnique(CPPPATH=[Dir(path)])


# get all the sourcefiles recursively
cpps = myenv.SGlob("*.cc", excludedirs=["test"], recursive=True )
# need this to go to the root dir, as SConsript asumes to be in the
# build dir
rootdir = myenv.Dir("#").abspath
libname = "casa_" + myenv["PACKAGE"]

if "static" in myenv["libtype"]:
    # build static lib
    slib =  myenv.StaticLibrary(target = libname, source = [cpps])
    installer.AddLibrary(slib)
if "shared" in myenv["libtype"]:
    #build dynamic lib
    dlib =  myenv.SharedLibrary(target = libname, source = cpps)
    installer.AddLibrary(dlib)

if myenv["tests"]:
    testenv = myenv.Copy()
    # AddCasaTests()  move the code belowto tools/casa.py
    # gather test files
    # SGlob works in build dir, so only for cc files
    tests = testenv.SGlob("test/*.cc")
    tests += testenv.SGlob("*/test/*.cc")
    # point to tests local includes
    # use static lib to link here
    #statlib = File(slib[0])
    # this doesn't seem to work in scons < 0.96.94
    #testenv.Append(LIBS=[statlib])
    testenv.AppendUnique(LIBPATH=[testenv["BUILDDIR"]])
    testenv.Prepend(LIBS=[libname])
    # store CPPPATH, to reset after each test 
    cpppath = testenv["CPPPATH"]
    # install assay *.run *.in* *.out files into test directory
    # use glob here with an absolute path, as Sconsript is "called" 
    # from the build dir.
    testaux = glob.glob("%s/%s/test/*.*" % (rootdir, testenv["PACKAGE"]))
    testaux += glob.glob("%s/%s/*/test/*.*" % (rootdir, testenv["PACKAGE"]))
    # exclude cc files
    testaux = [ i for i in testaux if not i.endswith('.cc') ]
    testaux = [ i for i in testaux if not i.endswith('.h') ]
    tdir = Dir("#/tests").abspath
    for i in testaux:
	# Copy doesn't overwrite, so check if they already exist 
	# from a previous run.
	if not os.path.exists(tdir):
	    testenv.Execute(Mkdir(tdir))
	outfile = os.path.join(tdir, os.path.basename(i))
	if not os.path.exists(outfile):
	    testenv.Execute(Copy(outfile, i))
 
    for i in tests:
	testenv["CPPPATH"] = cpppath
	# get the basename no suffix of the file
	tinfile = os.path.splitext(os.path.basename(i))[0]
	tdir = os.path.split(i)[0]
	# include location of test headers
	testenv.AppendUnique(CPPPATH=[tdir])			
	# output goes into the root/tests directory
	toutfile = os.path.join("#","tests", tinfile)
	tapp = testenv.Program(toutfile, source=i)

	# run assay here ....
	f=File(toutfile).abspath
	# dummy source to force execution for each test
	dummy = File("#/tests/%s.dummy" % f)
	ldpath = "LD_LIBRARY_PATH"
	if testenv["PLATFORM"] == 'darwin':
	    ldpath = "DYLD_LIBRARY_PATH"
	testenv["ASSAYENV"] = \
	    'AIPSPATH="tests" %s=%s' % \
	    (ldpath, libpath2ldpath(testenv["LIBPATH"]))
	testenv["ASSAY"] = os.path.join(testenv["casacoredir"], 
					"bin", 
					"casacore_assay")
	testenv.Command(dummy, None, ['@cd tests', 
					 '-@$ASSAYENV $ASSAY %s' % f])

# install headers, only works with absolute dir.
installer.AddHeaders( rootdir, "*.h", "casacore", True )
installer.AddHeaders( rootdir, "*.tcc", "casacore", True )
